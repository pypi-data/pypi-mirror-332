IF(Illyrian_FIND_VERSION AND Illyrian_VERSION AND Illyrian_VERSION VERSION_LESS Illyrian_FIND_VERSION)
	UNSET(Illyrian_VERSION CACHE)
	UNSET(Illyrian_FOUND CACHE)
ENDIF()
IF(NOT Illyrian_FOUND)
	SET(Illyrian_FOUND FALSE)
	FIND_PATH(Illyrian_DIR "__main__.py" PATHS "${CMAKE_CURRENT_LIST_DIR}/../")
 
	IF(NOT Illyrian_DIR)
		FIND_PACKAGE(Python3 3.7 REQUIRED)
		SET(Python3_USERLIB "$ENV{HOME}/.local/lib/python${Python3_VERSION_MAJOR}.${Python3_VERSION_MINOR}/site-packages")
		FIND_FILE(Illyrian_DIR "__main__.py" PATHS "${Python3_SITELIB}/illyrian" "${Python3_USERLIB}/illyrian")
	ENDIF()
 
	IF(Illyrian_DIR)
		FIND_FILE(Illyrian_MAIN "__main__.py" "PATHS" "${Illyrian_DIR}")
		FIND_FILE(Illyrian_CMAKE "illyrian.cmake" "PATHS" "${Illyrian_DIR}/cmake")

		IF(Illyrian_MAIN AND Illyrian_CMAKE)
			SET(Illyrian_FOUND TRUE)
			FILE(READ "${Illyrian_DIR}/version.py" Illyrian_VERSION)
			STRING(REGEX MATCH "__version__ = '([0-9\.]+)'" Illyrian_VERSION ${Illyrian_VERSION})
			SET(Illyrian_VERSION ${CMAKE_MATCH_1})
			SET(Illyrian_VERSION ${Illyrian_VERSION} CACHE STRING "Illyrian Version")
			MESSAGE(STATUS "Found Illyrian v${Illyrian_VERSION}: ${Illyrian_DIR}")
		ELSE()
			MESSAGE(STATUS "Unable to find all requirements of Illyrian in ${Illyrian_DIR}")
			MESSAGE(STATUS "Illyrian_MAIN: ${Illyrian_MAIN}")
			MESSAGE(STATUS "Illyrian_CMAKE: ${Illyrian_CMAKE}")
		ENDIF()
 
		SET(Illyrian_FOUND ${Illyrian_FOUND} CACHE BOOL "Found Illyrian" FORCE)
		MARK_AS_ADVANCED(Illyrian_FOUND Illyrian_VERSION Illyrian_DIR Illyrian_MAIN Illyrian_CMAKE)
	ENDIF()
ENDIF()
 
IF(Illyrian_FIND_REQUIRED AND NOT Illyrian_FOUND)
	MESSAGE(FATAL_ERROR "Unable to find Illyrian!")
ENDIF()
 
IF(Illyrian_FIND_VERSION AND Illyrian_VERSION VERSION_LESS Illyrian_FIND_VERSION)
	MESSAGE(FATAL_ERROR "Found Illyrian v${Illyrian_VERSION} but expected at least v${Illyrian_FIND_VERSION}")
ENDIF()
 
STRING(ASCII 59 Illyrian_SEMICOLON)
STRING(REPLACE "." "${Illyrian_SEMICOLON}" Illyrian_VERSION_LIST ${Illyrian_VERSION})
SET(Illyrian_VERSION_MAJOR 0)
SET(Illyrian_VERSION_MINOR 0)
SET(Illyrian_VERSION_PATCH 0)
LIST(LENGTH Illyrian_VERSION_LIST Illyrian_VERSION_LEN)
IF(Illyrian_VERSION_LEN GREATER 0)
	LIST(GET Illyrian_VERSION_LIST 0 Illyrian_VERSION_MAJOR)
ENDIF()
IF(Illyrian_VERSION_LEN GREATER 1)
	LIST(GET Illyrian_VERSION_LIST 1 Illyrian_VERSION_MINOR)
ENDIF()
IF(Illyrian_VERSION_LEN GREATER 2)
	LIST(GET Illyrian_VERSION_LIST 2 Illyrian_VERSION_PATCH)
ENDIF()
UNSET(Illyrian_VERSION_LEN)
UNSET(Illyrian_VERSION_LIST)
UNSET(Illyrian_SEMICOLON)
 
IF(Illyrian_FOUND)
	INCLUDE(${Illyrian_CMAKE})
ENDIF()